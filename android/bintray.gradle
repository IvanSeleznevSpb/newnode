apply plugin: 'com.jfrog.bintray'

version = android.defaultConfig.versionName

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompiler.classpath
        }
    }
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'NewNode'
        name = 'newnode'
        userOrg = 'clostra'
        desc = ''
        websiteUrl = 'https://github.com/clostra/newnode'
        vcsUrl = 'https://github.com/clostra/newnode.git'
        licenses = ["GPL-2.0"]
        publish = true
        version {
            desc = ''
            vcsTag = 'v' + version
            gpg.sign = true
        }
    }
}
