apply plugin: 'com.android.application'

android {
    compileSdkVersion 31

    new File(System.getenv('NDK') + '/source.properties').eachLine { line ->
        def matcher = line =~ /Pkg.Revision = (.*)/
        if (matcher) {
            ndkVersion = matcher[0][1]
            return true
        }
    }

    signingConfigs {
       release {
           storeFile file(RELEASE_STORE_FILE)
           storePassword RELEASE_STORE_PASSWORD
           keyAlias RELEASE_KEY_ALIAS
           keyPassword RELEASE_KEY_PASSWORD
           v1SigningEnabled true
           v2SigningEnabled true
       }
    }

    defaultConfig {
        applicationId "com.clostra.newnode.vpn"
        minSdkVersion 29
        targetSdkVersion 31

        /*
        new File('../../constants.h').eachLine { line ->
            def matcher = line =~ /VERSION "(.*)"/
            if (matcher) {
                versionName matcher[0][1]
                return true
            }
        }
        */
        versionName "2.0.0"
        versionCode versionName.replace(".", "").toInteger()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // include AAR directly (must include dependencies)
    implementation project(':newnode')
    implementation 'com.bugsnag:bugsnag-android:4.5.0'
    implementation 'com.google.android.gms:play-services-nearby:17.+'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'

    // use jcenter
    //implementation 'com.clostra.newnode:newnode:+'
}
