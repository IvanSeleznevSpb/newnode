buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.+'
        classpath 'com.bugsnag:bugsnag-android-gradle-plugin:3.+'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

allprojects {
    repositories {
        maven { url 'https://maven.google.com' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.bugsnag.android.gradle'

bugsnag {
    apiKey "141ea25aa72c276c49d3a154b82f2b1f"
    ndk true
    sharedObjectPath android.sourceSets.main.jniLibs.source[0]
}

android {
    compileSdkVersion = 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        new File('../constants.h').eachLine { line ->
            def matcher = line =~ /VERSION "(.*)"/
            if (matcher) {
                versionName matcher[0][1]
                return true
            }
        }
        versionCode versionName.replace(".", "").toInteger()
        buildConfigField("String", "BUGSNAG_API_KEY", "\"${project.bugsnag.apiKey}\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.bugsnag:bugsnag-android-ndk:1.+'
}

apply from: 'maven.gradle'
apply from: 'bintray.gradle'
